import org.codehaus.groovy.runtime.GStringImpl
import org.apache.tools.ant.taskdefs.condition.Os

buildscript {
    repositories {
        maven { url "https://maven.aliyun.com/repository/public/" } // Aliyun Mirrors
        maven { url "https://maven.aliyun.com/repository/gradle-plugin/" }
        mavenLocal()
        mavenCentral()
    }

    dependencies {
        classpath "org.openjfx:javafx-plugin:0.0.13"
        classpath "com.diffplug.spotless:spotless-plugin-gradle:7.0.0.BETA2"
    }
}


allprojects {
    apply plugin: "eclipse"
    apply plugin: "java-library"
    apply plugin: "org.openjfx.javafxplugin"
    apply plugin: "com.diffplug.spotless"

    version = "3.2.0"
    ext {
        // App Metadata
        appName = "ArkPets"
        appAuthor = "Harry Huang"
        appYearBegin = "2022"
        appYearCurrent = new Date().format("yyyy", TimeZone.getTimeZone("GMT+0:00"))
        appCopyright = "Copyright (c) ${appYearBegin}-${appYearCurrent} ${appAuthor}" as GStringImpl
        // Prefabs
        gdxVersion = "1.11.0"
        jnaVersion = "5.12.1"
        javaFXVersion = "17.0.8"
    }

    repositories {
        maven { url "https://maven.aliyun.com/repository/public/" } // Aliyun Mirrors
        mavenLocal()
        mavenCentral()
    }

    spotless {

    }
}


project(":desktop") {
    dependencies {
        implementation project(":core")
        // libGDX Desktop
        api "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-desktop"
        api "com.badlogicgames.gdx:gdx-freetype-platform:$gdxVersion:natives-desktop"
    }
}


project(":core") {
    dependencies {
        // Spine Runtime
        api "com.esotericsoftware.spine:spine-libgdx:3.8.99.1"
        // libGDX
        api "com.badlogicgames.gdx:gdx:$gdxVersion"
        api "com.badlogicgames.gdx:gdx-backend-lwjgl3:$gdxVersion"
        // JNA
        api "net.java.dev.jna:jna:$jnaVersion"
        api "net.java.dev.jna:jna-platform:$jnaVersion"
        // JavaFX
        api "org.openjfx:javafx-base:$javaFXVersion:win"
        api "org.openjfx:javafx-controls:$javaFXVersion:win"
        api "org.openjfx:javafx-graphics:$javaFXVersion:win"
        api "org.openjfx:javafx-fxml:$javaFXVersion:win"
        // JFoenix
        api "com.jfoenix:jfoenix:9.0.1"
        // FastJson
        api "com.alibaba:fastjson:2.0.39"
        // Log4j
        api "apache-log4j:log4j:1.2.15"
    }
}

task installGitHooks(type: Copy, group: 'build setup') {
    println 'installGitHooks task'
    def platform = "linux"
    if (Os.isFamily(Os.FAMILY_WINDOWS)) {
        platform = "windows"
    }
    from("docs/hooks/$platform") {
        include "**"
    }
    into ".git/hooks"
}
